***README***

An open source ActionScript 3 library for generating Squarified Treemaps.

**Usage:**

The example below uses a MapItem class that implements the ITreeMapData interface. 
The weight property on that interface is used to generate the relative size of the individual rectangles

Custom renderers can be used to draw the rectangles by extending the Treemap class and overriding the draw function. 

<code>

	package
	{
		import com.arpitonline.treemap.TreeMap;

		import flash.display.Sprite;
		import flash.events.Event;

		public class SampleTreemapProject extends Sprite
		{

			private var tm:TreeMap = new TreeMap();

			public function SampleTreemapProject()
			{
				stage.scaleMode = "noScale";
				stage.align = "TL";


				tm.setSize(stage.stageWidth, stage.stageHeight);

				var dp:Array = [];
				for(var i:int = 0; i < 5; i++){
					var i1:MapItem = new MapItem("Item_"+i, 4 );
					dp.push(i1);

				}

				tm.dataProvider = dp;
				addChild(tm);

				stage.addEventListener(Event.RESIZE, onStageResize);

			}

			private function onStageResize(event:Event):void{
				tm.setSize(stage.stageWidth, stage.stageHeight);
				tm.layout();
			}
		}
	}

	import com.arpitonline.treemap.ITreeMapData;

	class MapItem implements ITreeMapData{

		private var _label:String;
		private var _weight:Number
		public function MapItem(label:String, weight:Number){
			_label = label;
			_weight = weight;
		}

		public function get weight():Number{
			return _weight;	
		}
	}
	
			
</code>